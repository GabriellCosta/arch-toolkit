buildscript {
    apply from: "$rootDir/tools/versions.gradle"

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jacoco:org.jacoco.core:$versions.jacoco"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$versions.dexcount"

        // Necessary to publish
//        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$versions.bintray_plugin"
//        classpath "com.github.dcendents:android-maven-gradle-plugin:$versions.maven_plugin"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    apply from: "$rootDir/tools/dependencies/unit-test.gradle"
    apply from: "$rootDir/tools/dependencies/instrumented-test.gradle"

    afterEvaluate {

        if (!project.hasProperty("android")) return

        android {

            compileSdkVersion 32
            buildToolsVersion "32.0.0"

            defaultConfig {
                minSdkVersion 23
                targetSdkVersion 32

                versionCode 1
                versionName "1.0"

                resConfigs "en"
            }

            lintOptions {
                checkReleaseBuilds true

                abortOnError true
                ignoreWarnings false
                absolutePaths false
                warningsAsErrors false

                htmlOutput file("$rootDir/build/reports/lint/html/$name-lint.html")
                xmlOutput file("$rootDir/build/reports/lint/xml/$name-lint.xml")
            }

            aaptOptions {
                cruncherEnabled = false
            }

            testOptions {
                unitTests.includeAndroidResources true
                unitTests.returnDefaultValues = true
                animationsDisabled true
            }

            packagingOptions {
                exclude 'META-INF/LICENSE'
                pickFirst 'protobuf.meta'
                doNotStrip '*/mips/*.so'
                doNotStrip '*/mips64/*.so'
            }

            sourceSets {
                main.java.srcDirs += 'src/main/kotlin'
                test.java.srcDirs += 'src/test/kotlin'
                androidTest.java.srcDirs += 'src/androidTest/kotlin'
                androidTest.resources.srcDirs += 'src/androidTest/res'
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }


            task sourcesJar(type: Jar) {
                from android.sourceSets.main.java.srcDirs
                classifier = 'sources'
            }

            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
                options.addStringOption('encoding', 'UTF-8')
            }

            configurations {
                javadocDeps
            }

            task javadoc(type: Javadoc) {
                excludes = ['**/*.kt']
                source = android.sourceSets.main.java.srcDirs
                classpath += configurations.javadocDeps
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            }

            task javadocJar(type: Jar, dependsOn: javadoc) {
                classifier = 'javadoc'
                from javadoc.destinationDir
            }

            tasks.withType(Test) {
                jacoco.includeNoLocationClasses = true
                jacoco.excludes = ['jdk.internal.*']
            }

            task publishRelease(group: "publishing",
                    dependsOn: ["clean", "build", "install", "publish"])

            artifacts {
                archives javadocJar
                archives sourcesJar
            }

            dependencies {
                javadocDeps "com.android.support:support-annotations:$versions.android_support"
                ktlint "com.github.shyiko:ktlint:$versions.ktLint"
            }
        }
    }

    apply from: "$rootDir/tools/ktlint.gradle"
    apply from: "$rootDir/tools/coverage/jacoco.gradle"
    apply from: "$rootDir/tools/coverage/jacoco-unified.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
